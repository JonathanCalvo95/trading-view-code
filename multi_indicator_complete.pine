// 23EMAs + Koncorde + RSI + MACD [Dylan Argot]
//@version=6
indicator('23EMAs + Koncorde + RSI + MACD [Dylan Argot]', overlay=false)

// ╔══════════════════════════════════════════════════════════════════════╗
// ║                        INPUTS GENERALES                              ║
// ╚══════════════════════════════════════════════════════════════════════╝

// --- Visibilidad ---
showPerformance = input.bool(true, 'Mostrar Performance', group='Cuadro de Performance')
showTablaD      = input.bool(true, 'Mostrar Tabla Diaria', group='Tabla de datos')
showTablaS      = input.bool(true, 'Mostrar Tabla Semanal', group='Tabla de datos')
showTablaM      = input.bool(true, 'Mostrar Tabla Mensual', group='Tabla de datos')
showKoncorde    = input.bool(true, 'KONCORDE VISIBLE', group='Indicadores')
showRSI         = input.bool(true, 'RSI VISIBLE', group='Indicadores')
showMACD        = input.bool(true, 'MACD VISIBLE', group='Indicadores')

// --- Medias Móviles Diarias ---
GRP_D = 'Medias Moviles Diarias'
showD1 = input.bool(true, title='', group=GRP_D, inline='a')
tipoD1 = input.string('SMA', title='Media 1', inline='a', group=GRP_D, options=['SMA','EMA','WMA','RMA'])
lenD1  = input.int(21,  'De', inline='a', group=GRP_D)
colD1  = input.color(color.blue,   'Color', inline='a', group=GRP_D)

showD2 = input.bool(true, title='', group=GRP_D, inline='b')
tipoD2 = input.string('SMA', title='Media 2', inline='b', group=GRP_D, options=['SMA','EMA','WMA','RMA'])
lenD2  = input.int(30,  'De', inline='b', group=GRP_D)
colD2  = input.color(color.green,  'Color', inline='b', group=GRP_D)

showD3 = input.bool(true, title='', group=GRP_D, inline='c')
tipoD3 = input.string('SMA', title='Media 3', inline='c', group=GRP_D, options=['SMA','EMA','WMA','RMA'])
lenD3  = input.int(100, 'De', inline='c', group=GRP_D)
colD3  = input.color(color.yellow, 'Color', inline='c', group=GRP_D)

showD4 = input.bool(true, title='', group=GRP_D, inline='d')
tipoD4 = input.string('EMA', title='Media 4', inline='d', group=GRP_D, options=['SMA','EMA','WMA','RMA'])
lenD4  = input.int(150, 'De', inline='d', group=GRP_D)
colD4  = input.color(color.orange, 'Color', inline='d', group=GRP_D)

showD5 = input.bool(true, title='', group=GRP_D, inline='e')
tipoD5 = input.string('EMA', title='Media 5', inline='e', group=GRP_D, options=['SMA','EMA','WMA','RMA'])
lenD5  = input.int(200, 'De', inline='e', group=GRP_D)
colD5  = input.color(color.red,    'Color', inline='e', group=GRP_D)

fillD_alcista = input.color(#4caf4f4c, title='Color cuando MM1 > MM2', group=GRP_D)
fillD_bajista = input.color(#ff52524d, title='Color cuando MM2 > MM1', group=GRP_D)

// --- Medias Móviles Semanales ---
GRP_S = 'Medias Moviles Semanales'
showS1 = input.bool(true, title='', group=GRP_S, inline='j')
tipoS1 = input.string('EMA', title='Media 1', inline='j', group=GRP_S, options=['SMA','EMA','WMA','RMA'])
lenS1  = input.int(10,  'De', inline='j', group=GRP_S)
colS1  = input.color(color.blue,   'Color', inline='j', group=GRP_S)

showS2 = input.bool(true, title='', group=GRP_S, inline='k')
tipoS2 = input.string('EMA', title='Media 2', inline='k', group=GRP_S, options=['SMA','EMA','WMA','RMA'])
lenS2  = input.int(20,  'De', inline='k', group=GRP_S)
colS2  = input.color(color.green,  'Color', inline='k', group=GRP_S)

showS3 = input.bool(true, title='', group=GRP_S, inline='l')
tipoS3 = input.string('EMA', title='Media 3', inline='l', group=GRP_S, options=['SMA','EMA','WMA','RMA'])
lenS3  = input.int(30,  'De', inline='l', group=GRP_S)
colS3  = input.color(color.yellow, 'Color', inline='l', group=GRP_S)

showS4 = input.bool(true, title='', group=GRP_S, inline='m')
tipoS4 = input.string('EMA', title='Media 4', inline='m', group=GRP_S, options=['SMA','EMA','WMA','RMA'])
lenS4  = input.int(50,  'De', inline='m', group=GRP_S)
colS4  = input.color(color.orange, 'Color', inline='m', group=GRP_S)

showS5 = input.bool(true, title='', group=GRP_S, inline='n')
tipoS5 = input.string('EMA', title='Media 5', inline='n', group=GRP_S, options=['SMA','EMA','WMA','RMA'])
lenS5  = input.int(200, 'De', inline='n', group=GRP_S)
colS5  = input.color(color.red,    'Color', inline='n', group=GRP_S)

showS6 = input.bool(true, title='', group=GRP_S, inline='o')
tipoS6 = input.string('SMA', title='Media 6', inline='o', group=GRP_S, options=['SMA','EMA','WMA','RMA'])
lenS6  = input.int(200, 'De', inline='o', group=GRP_S)
colS6  = input.color(color.purple, 'Color', inline='o', group=GRP_S)

fillS_alcista = input.color(#4caf4f4c, title='Color cuando MM1 > MM2', group=GRP_S)
fillS_bajista = input.color(#ff52524d, title='Color cuando MM2 > MM1', group=GRP_S)

// --- Medias Móviles Mensuales ---
GRP_M = 'Medias Moviles Mensuales'
showM1 = input.bool(true, title='', group=GRP_M, inline='a')
tipoM1 = input.string('EMA', title='Media 1', inline='a', group=GRP_M, options=['SMA','EMA','WMA','RMA'])
lenM1  = input.int(10,  'De', inline='a', group=GRP_M)
colM1  = input.color(color.blue,   'Color', inline='a', group=GRP_M)

showM2 = input.bool(true, title='', group=GRP_M, inline='b')
tipoM2 = input.string('EMA', title='Media 2', inline='b', group=GRP_M, options=['SMA','EMA','WMA','RMA'])
lenM2  = input.int(20,  'De', inline='b', group=GRP_M)
colM2  = input.color(color.green,  'Color', inline='b', group=GRP_M)

showM3 = input.bool(true, title='', group=GRP_M, inline='c')
tipoM3 = input.string('EMA', title='Media 3', inline='c', group=GRP_M, options=['SMA','EMA','WMA','RMA'])
lenM3  = input.int(30,  'De', inline='c', group=GRP_M)
colM3  = input.color(color.yellow, 'Color', inline='c', group=GRP_M)

showM4 = input.bool(true, title='', group=GRP_M, inline='d')
tipoM4 = input.string('EMA', title='Media 4', inline='d', group=GRP_M, options=['SMA','EMA','WMA','RMA'])
lenM4  = input.int(40,  'De', inline='d', group=GRP_M)
colM4  = input.color(color.orange, 'Color', inline='d', group=GRP_M)

showM5 = input.bool(true, title='', group=GRP_M, inline='e')
tipoM5 = input.string('EMA', title='Media 5', inline='e', group=GRP_M, options=['SMA','EMA','WMA','RMA'])
lenM5  = input.int(200, 'De', inline='e', group=GRP_M)
colM5  = input.color(color.red,    'Color', inline='e', group=GRP_M)

// --- RSI ---
GRP_RSI = 'RSI'
rsiLenTabla = input.int(14, title='Longitud RSI (Tabla)', group=GRP_RSI)
rsiLenPlot  = input.int(9,  minval=1, title='Longitud RSI (Gráfico)', group=GRP_RSI)
rsiMALen    = input.int(21, title='Media RSI', group=GRP_RSI)

// --- MACD ---
GRP_MACD = 'MACD'
macdFastLen   = input.int(12, title='Longitud Media Rápida', group=GRP_MACD)
macdSlowLen   = input.int(26, title='Longitud Media Lenta', group=GRP_MACD)
macdSignalLen = input.int(9,  title='Longitud Suavizado', minval=1, maxval=50, group=GRP_MACD)


// ╔══════════════════════════════════════════════════════════════════════╗
// ║                     FUNCIONES AUXILIARES                             ║
// ╚══════════════════════════════════════════════════════════════════════╝

// Media Móvil genérica
f_ma(src, len, tipo) =>
    switch tipo
        'SMA' => ta.sma(src, len)
        'EMA' => ta.ema(src, len)
        'WMA' => ta.wma(src, len)
        =>       ta.rma(src, len)

// Porcentaje de distancia del precio respecto a una media
f_dist(ma) =>
    ((close - ma) / ma) * 100

// Color de fondo según porcentaje positivo/negativo
f_bgColor(pct) =>
    pct > 0 ? #367d39 : #8d2727

// Color según nivel de RSI
f_colorRSI(val) =>
    if val > 70
        color.red
    else if val >= 60
        color.orange
    else if val >= 40
        #53555d
    else if val >= 30
        #477003
    else
        color.lime

// Retorno porcentual
f_return(v1, v2) =>
    (v1 - v2) * 100 / math.abs(v2)

// Performance en timeframe diario
f_perf(barsBack) =>
    request.security(syminfo.tickerid, '1D', f_return(close, close[barsBack]))

// Retorno logarítmico (para Beta)
f_logReturn(src) =>
    math.log(src / src[1])

// MFI (para Koncorde)
f_mfi(len) =>
    100.0 - 100.0 / (1.0 + math.sum(volume * (ta.change(hlc3) <= 0 ? 0 : hlc3), len) / math.sum(volume * (ta.change(hlc3) >= 0 ? 0 : hlc3), len))

// Fila de tabla de datos: encabezado + porcentaje
f_tablaFila(tbl, col, tipo, periodo, dist) =>
    table.cell(tbl, col, 0, text=tipo + ' ' + str.tostring(periodo), text_color=color.white, bgcolor=#000000)
    table.cell(tbl, col, 1, str.tostring(dist, '#.00') + '%', text_color=color.white, bgcolor=f_bgColor(dist))

// Encabezado de performance
f_perfHeader(tbl, col, row, txt) =>
    table.cell(tbl, col, row, txt, bgcolor=#000000, text_color=color.white)

// Celda de performance con color
f_perfCell(tbl, col, row, val) =>
    c = color.new(val >= 0 ? color.rgb(15, 68, 37) : #9b1d1d, 0)
    table.cell(tbl, col, row, str.tostring(val, '0.00') + '%\n', bgcolor=c, text_color=color.white)


// ╔══════════════════════════════════════════════════════════════════════╗
// ║                 CÁLCULOS - MEDIAS MÓVILES                            ║
// ╚══════════════════════════════════════════════════════════════════════╝

esD = timeframe.isdaily
esS = timeframe.isweekly
esM = timeframe.ismonthly

// Diarias
maD1 = esD ? f_ma(close, lenD1, tipoD1) : na
maD2 = esD ? f_ma(close, lenD2, tipoD2) : na
maD3 = esD ? f_ma(close, lenD3, tipoD3) : na
maD4 = esD ? f_ma(close, lenD4, tipoD4) : na
maD5 = esD ? f_ma(close, lenD5, tipoD5) : na

// Semanales
maS1 = esS ? f_ma(close, lenS1, tipoS1) : na
maS2 = esS ? f_ma(close, lenS2, tipoS2) : na
maS3 = esS ? f_ma(close, lenS3, tipoS3) : na
maS4 = esS ? f_ma(close, lenS4, tipoS4) : na
maS5 = esS ? f_ma(close, lenS5, tipoS5) : na
maS6 = esS ? f_ma(close, lenS6, tipoS6) : na

// Mensuales
maM1 = esM ? f_ma(close, lenM1, tipoM1) : na
maM2 = esM ? f_ma(close, lenM2, tipoM2) : na
maM3 = esM ? f_ma(close, lenM3, tipoM3) : na
maM4 = esM ? f_ma(close, lenM4, tipoM4) : na
maM5 = esM ? f_ma(close, lenM5, tipoM5) : na


// ╔══════════════════════════════════════════════════════════════════════╗
// ║               PLOTS - MEDIAS MÓVILES + LABELS                        ║
// ╚══════════════════════════════════════════════════════════════════════╝

barOffset = time - time[1]

// --- Diarias: fill entre MA1 y MA2 ---
pD1 = plot(maD1, color=#00000000, editable=false, force_overlay=true)
pD2 = plot(maD2, color=#00000000, editable=false, force_overlay=true)
fill(pD1, pD2, color=(maD1 > maD2 ? fillD_alcista : fillD_bajista))

plot(showD1 and esD ? maD1 : na, title='MA 1 Diaria', color=colD1, linewidth=1, force_overlay=true)
plot(showD2 and esD ? maD2 : na, title='MA 2 Diaria', color=colD2, linewidth=1, force_overlay=true)
plot(showD3 and esD ? maD3 : na, title='MA 3 Diaria', color=colD3, linewidth=1, force_overlay=true)
plot(showD4 and esD ? maD4 : na, title='MA 4 Diaria', color=colD4, linewidth=1, force_overlay=true)
plot(showD5 and esD ? maD5 : na, title='MA 5 Diaria', color=colD5, linewidth=1, force_overlay=true)

if showD1
    lblD1 = label.new(time + barOffset, maD1, xloc=xloc.bar_time, text=tipoD1 + ' ' + str.tostring(lenD1), color=color.new(color.red, 100), style=label.style_label_lower_left, size=size.normal, textcolor=colD1, force_overlay=true)
    label.delete(lblD1[1])
if showD2
    lblD2 = label.new(time + barOffset, maD2 - maD2 * 0.001, xloc=xloc.bar_time, text=tipoD2 + ' ' + str.tostring(lenD2), color=color.new(color.red, 100), style=label.style_label_lower_left, size=size.normal, textcolor=colD2, force_overlay=true)
    label.delete(lblD2[1])
if showD3
    lblD3 = label.new(time + barOffset, maD3 - maD3 * 0.001, xloc=xloc.bar_time, text=tipoD3 + ' ' + str.tostring(lenD3), color=color.new(color.red, 100), style=label.style_label_lower_left, size=size.normal, textcolor=colD3, force_overlay=true)
    label.delete(lblD3[1])
if showD4
    lblD4 = label.new(time + barOffset, maD4 - maD4 * 0.001, xloc=xloc.bar_time, text=tipoD4 + ' ' + str.tostring(lenD4), color=color.new(color.red, 100), style=label.style_label_lower_left, size=size.normal, textcolor=colD4, force_overlay=true)
    label.delete(lblD4[1])
if showD5
    lblD5 = label.new(time + barOffset, maD5 - maD5 * 0.001, xloc=xloc.bar_time, text=tipoD5 + ' ' + str.tostring(lenD5), color=color.new(color.red, 100), style=label.style_label_lower_left, size=size.normal, textcolor=colD5, force_overlay=true)
    label.delete(lblD5[1])

// --- Semanales: fill entre MA1 y MA2 ---
pS1 = plot(maS1, color=#00000000, editable=false, force_overlay=true)
pS2 = plot(maS2, color=#00000000, editable=false, force_overlay=true)
fill(pS1, pS2, color=(maS1 > maS2 ? fillS_alcista : fillS_bajista))

plot(showS1 and esS ? maS1 : na, title='MA 1 Semanal', color=colS1, linewidth=1, force_overlay=true)
plot(showS2 and esS ? maS2 : na, title='MA 2 Semanal', color=colS2, linewidth=1, force_overlay=true)
plot(showS3 and esS ? maS3 : na, title='MA 3 Semanal', color=colS3, linewidth=1, force_overlay=true)
plot(showS4 and esS ? maS4 : na, title='MA 4 Semanal', color=colS4, linewidth=1, force_overlay=true)
plot(showS5 and esS ? maS5 : na, title='MA 5 Semanal', color=colS5, linewidth=1, force_overlay=true)
plot(showS6 and esS ? maS6 : na, title='MA 6 Semanal (SMA 200)', color=colS6, linewidth=1, force_overlay=true)

if showS1
    lblS1 = label.new(time + barOffset, maS1, xloc=xloc.bar_time, text=tipoS1 + ' ' + str.tostring(lenS1), color=color.new(color.red, 100), style=label.style_label_lower_left, size=size.normal, textcolor=colS1, force_overlay=true)
    label.delete(lblS1[1])
if showS2
    lblS2 = label.new(time + barOffset, maS2 - maS2 * 0.001, xloc=xloc.bar_time, text=tipoS2 + ' ' + str.tostring(lenS2), color=color.new(color.red, 100), style=label.style_label_lower_left, size=size.normal, textcolor=colS2, force_overlay=true)
    label.delete(lblS2[1])
if showS3
    lblS3 = label.new(time + barOffset, maS3 - maS3 * 0.001, xloc=xloc.bar_time, text=tipoS3 + ' ' + str.tostring(lenS3), color=color.new(color.red, 100), style=label.style_label_lower_left, size=size.normal, textcolor=colS3, force_overlay=true)
    label.delete(lblS3[1])
if showS4
    lblS4 = label.new(time + barOffset, maS4 - maS4 * 0.001, xloc=xloc.bar_time, text=tipoS4 + ' ' + str.tostring(lenS4), color=color.new(color.red, 100), style=label.style_label_lower_left, size=size.normal, textcolor=colS4, force_overlay=true)
    label.delete(lblS4[1])
if showS5
    lblS5 = label.new(time + barOffset, maS5 - maS5 * 0.001, xloc=xloc.bar_time, text=tipoS5 + ' ' + str.tostring(lenS5), color=color.new(color.red, 100), style=label.style_label_lower_left, size=size.normal, textcolor=colS5, force_overlay=true)
    label.delete(lblS5[1])
if showS6
    lblS6 = label.new(time + barOffset, maS6 - maS6 * 0.001, xloc=xloc.bar_time, text=tipoS6 + ' ' + str.tostring(lenS6), color=color.new(color.red, 100), style=label.style_label_lower_left, size=size.normal, textcolor=colS6, force_overlay=true)
    label.delete(lblS6[1])

// --- Mensuales: plots (sin fill) ---
plot(showM1 and esM ? maM1 : na, title='MA 1 Mensual', color=colM1, linewidth=1, force_overlay=true)
plot(showM2 and esM ? maM2 : na, title='MA 2 Mensual', color=colM2, linewidth=1, force_overlay=true)
plot(showM3 and esM ? maM3 : na, title='MA 3 Mensual', color=colM3, linewidth=1, force_overlay=true)
plot(showM4 and esM ? maM4 : na, title='MA 4 Mensual', color=colM4, linewidth=1, force_overlay=true)
plot(showM5 and esM ? maM5 : na, title='MA 5 Mensual', color=colM5, linewidth=1, force_overlay=true)

if showM1
    lblM1 = label.new(time + barOffset, maM1, xloc=xloc.bar_time, text=tipoM1 + ' ' + str.tostring(lenM1), color=color.new(color.red, 100), style=label.style_label_lower_left, size=size.normal, textcolor=colM1, force_overlay=true)
    label.delete(lblM1[1])
if showM2
    lblM2 = label.new(time + barOffset, maM2 - maM2 * 0.001, xloc=xloc.bar_time, text=tipoM2 + ' ' + str.tostring(lenM2), color=color.new(color.red, 100), style=label.style_label_lower_left, size=size.normal, textcolor=colM2, force_overlay=true)
    label.delete(lblM2[1])
if showM3
    lblM3 = label.new(time + barOffset, maM3 - maM3 * 0.001, xloc=xloc.bar_time, text=tipoM3 + ' ' + str.tostring(lenM3), color=color.new(color.red, 100), style=label.style_label_lower_left, size=size.normal, textcolor=colM3, force_overlay=true)
    label.delete(lblM3[1])
if showM4
    lblM4 = label.new(time + barOffset, maM4 - maM4 * 0.001, xloc=xloc.bar_time, text=tipoM4 + ' ' + str.tostring(lenM4), color=color.new(color.red, 100), style=label.style_label_lower_left, size=size.normal, textcolor=colM4, force_overlay=true)
    label.delete(lblM4[1])
if showM5
    lblM5 = label.new(time + barOffset, maM5 - maM5 * 0.001, xloc=xloc.bar_time, text=tipoM5 + ' ' + str.tostring(lenM5), color=color.new(color.red, 100), style=label.style_label_lower_left, size=size.normal, textcolor=colM5, force_overlay=true)
    label.delete(lblM5[1])


// ╔══════════════════════════════════════════════════════════════════════╗
// ║                   TABLA DE PERFORMANCE                               ║
// ╚══════════════════════════════════════════════════════════════════════╝

var table tblPerf = table.new(position.top_right, 6, 2, border_width=1, force_overlay=true)
lastYearClose = request.security(syminfo.tickerid, '12M', close[1], lookahead=barmerge.lookahead_on)

if showPerformance
    f_perfHeader(tblPerf, 0, 0, '1 Semana')
    f_perfHeader(tblPerf, 1, 0, '1 Mes')
    f_perfHeader(tblPerf, 2, 0, '3 Meses')
    f_perfHeader(tblPerf, 3, 0, '6 Meses')
    f_perfHeader(tblPerf, 4, 0, 'Este Año')
    f_perfHeader(tblPerf, 5, 0, '1 Año')
    f_perfCell(tblPerf, 0, 1, f_perf(5))
    f_perfCell(tblPerf, 1, 1, f_perf(21))
    f_perfCell(tblPerf, 2, 1, f_perf(63))
    f_perfCell(tblPerf, 3, 1, f_perf(126))
    f_perfCell(tblPerf, 4, 1, f_return(close, lastYearClose))
    f_perfCell(tblPerf, 5, 1, f_perf(251))


// ╔══════════════════════════════════════════════════════════════════════╗
// ║          TABLA DE DATOS (Distancia a MAs + RSI + Beta)               ║
// ╚══════════════════════════════════════════════════════════════════════╝

rsiTabla = ta.rsi(close, rsiLenTabla)
var table tblDatos = table.new(position.bottom_right, 9, 9, bgcolor=#242424, force_overlay=true)

// --- Cálculo de Beta (para tabla diaria) ---
betaLen    = 60
instrument = request.security(syminfo.tickerid, timeframe.period, close)
benchmark  = request.security('SPY', timeframe.period, close)
instRet    = f_logReturn(instrument)
benchRet   = f_logReturn(benchmark)
avgInst    = ta.sma(instRet, betaLen)
avgBench   = ta.sma(benchRet, betaLen)

covSum = 0.0
for i = 0 to betaLen - 1
    covSum += (instRet[i] - avgInst) * (benchRet[i] - avgBench)
beta = (covSum / (betaLen - 1)) / ta.variance(benchRet, betaLen)

// --- Tabla Diaria ---
if esD and showTablaD
    f_tablaFila(tblDatos, 0, tipoD1, lenD1, f_dist(maD1))
    f_tablaFila(tblDatos, 1, tipoD2, lenD2, f_dist(maD2))
    f_tablaFila(tblDatos, 2, tipoD3, lenD3, f_dist(maD3))
    f_tablaFila(tblDatos, 3, tipoD4, lenD4, f_dist(maD4))
    f_tablaFila(tblDatos, 4, tipoD5, lenD5, f_dist(maD5))
    table.cell(tblDatos, 6, 0, text='RSI', text_color=color.white, bgcolor=#000000)
    table.cell(tblDatos, 6, 1, str.tostring(rsiTabla, '#'), text_color=color.white, bgcolor=f_colorRSI(rsiTabla))
    table.cell(tblDatos, 7, 0, text='β', text_color=color.white, bgcolor=#000000)
    table.cell(tblDatos, 7, 1, str.tostring(beta, '#.#'), text_color=color.white, bgcolor=#53555d)
    for c = 0 to 7
        table.cell(tblDatos, c, 3, text=(c == 3 ? 'By Dylan Argot' : ''), text_color=color.white, bgcolor=#000000)

// --- Tabla Semanal ---
if esS and showTablaS
    f_tablaFila(tblDatos, 0, tipoS1, lenS1, f_dist(maS1))
    f_tablaFila(tblDatos, 1, tipoS2, lenS2, f_dist(maS2))
    f_tablaFila(tblDatos, 2, tipoS3, lenS3, f_dist(maS3))
    f_tablaFila(tblDatos, 3, tipoS4, lenS4, f_dist(maS4))
    f_tablaFila(tblDatos, 4, tipoS5, lenS5, f_dist(maS5))
    f_tablaFila(tblDatos, 5, tipoS6, lenS6, f_dist(maS6))
    table.cell(tblDatos, 6, 0, text='RSI', text_color=color.white, bgcolor=#000000)
    table.cell(tblDatos, 6, 1, str.tostring(rsiTabla, '#'), text_color=color.white, bgcolor=f_colorRSI(rsiTabla))
    for c = 0 to 6
        table.cell(tblDatos, c, 3, text=(c == 3 ? 'By Dylan Argot' : ''), text_color=color.white, bgcolor=#000000)

// --- Tabla Mensual ---
if esM and showTablaM
    f_tablaFila(tblDatos, 0, tipoM1, lenM1, f_dist(maM1))
    f_tablaFila(tblDatos, 1, tipoM2, lenM2, f_dist(maM2))
    f_tablaFila(tblDatos, 2, tipoM3, lenM3, f_dist(maM3))
    f_tablaFila(tblDatos, 3, tipoM4, lenM4, f_dist(maM4))
    f_tablaFila(tblDatos, 4, tipoM5, lenM5, f_dist(maM5))
    table.cell(tblDatos, 5, 0, text='RSI', text_color=color.white, bgcolor=#000000)
    table.cell(tblDatos, 5, 1, str.tostring(rsiTabla, '#'), text_color=color.white, bgcolor=f_colorRSI(rsiTabla))
    for c = 0 to 5
        table.cell(tblDatos, c, 3, text=(c == 3 ? 'By Dylan Argot' : ''), text_color=color.white, bgcolor=#000000)


// ╔══════════════════════════════════════════════════════════════════════╗
// ║                          KONCORDE                                    ║
// ╚══════════════════════════════════════════════════════════════════════╝

SCALE_K = esD ? 3.0 : esS ? 6.0 : 9.0
DELTA_K = -15

tprice  = ohlc4
mKonc   = 15

// PVI / NVI
pvim_k = ta.ema(ta.pvi, mKonc)
oscp   = (ta.pvi - pvim_k) * 100 / (ta.highest(pvim_k, 90) - ta.lowest(pvim_k, 90))
nvim_k = ta.ema(ta.nvi, mKonc)
azul   = (ta.nvi - nvim_k) * 100 / (ta.highest(nvim_k, 90) - ta.lowest(nvim_k, 90))

// Componentes
xmf     = f_mfi(14)
bbBasis = ta.sma(tprice, 25)
bbDev   = 2.0 * ta.stdev(tprice, 25)
BollOsc = (tprice - bbBasis) / (2.0 * bbDev) * 100
xrsi_k  = ta.rsi(tprice, 14)
stoc    = ta.sma(100 * (tprice - ta.lowest(low, 21)) / (ta.highest(high, 21) - ta.lowest(low, 21)), 3)

// Señales
marron = (xrsi_k + xmf + BollOsc + stoc / 3) / 2
verde  = marron + oscp
mediaK = ta.ema(marron, mKonc)

// Plots Koncorde
plot(showKoncorde ? verde  / SCALE_K + DELTA_K : na, color=color.new(#00ff00, 0), style=plot.style_columns, histbase=DELTA_K, linewidth=2, title='Manos Chicas')
plot(showKoncorde ? marron / SCALE_K + DELTA_K : na, color=color.new(#ffb974, 0), style=plot.style_columns, histbase=DELTA_K, linewidth=2, title='Montaña')
plot(showKoncorde ? azul   / SCALE_K + DELTA_K : na, color=#330072, style=plot.style_columns, histbase=DELTA_K, linewidth=2, title='Manos Grandes')
plot(showKoncorde ? marron / SCALE_K + DELTA_K : na, color=color.rgb(116, 56, 0), linewidth=2, title='Línea Montaña')
plot(showKoncorde ? verde  / SCALE_K + DELTA_K : na, color=color.new(#008c00, 0), linewidth=2, title='Línea Manos Chicas')
plot(showKoncorde ? azul   / SCALE_K + DELTA_K : na, color=color.new(#0095ff, 0), linewidth=2, title='Línea Manos Grandes')
plot(showKoncorde ? mediaK / SCALE_K + DELTA_K : na, color=color.rgb(255, 0, 0), linewidth=2, title='Media Móvil Koncorde')
plot(showKoncorde ? float(DELTA_K) : na, color=color.new(color.white, 0), linewidth=1, title='Cero Koncorde')


// ╔══════════════════════════════════════════════════════════════════════╗
// ║                            MACD                                      ║
// ╚══════════════════════════════════════════════════════════════════════╝

SCALE_MACD = esD ? 3.5 : esS ? 8.0 : 12.0
DELTA_MACD = -125

COL_GROW_UP = #26A69A
COL_FALL_UP = #B2DFDB
COL_FALL_DN = #EF5350
COL_GROW_DN = #FFCDD2
COL_MACD    = #0094ff
COL_SIGNAL  = #ff6a00

macdFast   = ta.ema(close, macdFastLen)
macdSlow   = ta.ema(close, macdSlowLen)
macdLine   = (macdFast - macdSlow) / macdSlow * 1000
signalLine = ta.ema(macdLine, macdSignalLen)
macdHist   = macdLine - signalLine

histColor = macdHist >= 0 ? (macdHist[1] < macdHist ? COL_GROW_UP : COL_FALL_UP) : (macdHist[1] < macdHist ? COL_GROW_DN : COL_FALL_DN)

plot(showMACD ? macdHist   / SCALE_MACD + DELTA_MACD : na, title='Histograma MACD', style=plot.style_columns, color=histColor, histbase=DELTA_MACD)
plot(showMACD ? macdLine   / SCALE_MACD + DELTA_MACD : na, title='MACD', color=color.new(COL_MACD, 0))
plot(showMACD ? signalLine / SCALE_MACD + DELTA_MACD : na, title='Señal MACD', color=color.new(COL_SIGNAL, 0))
plot(showMACD ? float(DELTA_MACD) : na, color=color.new(color.white, 0), linewidth=1, title='Cero MACD')


// ╔══════════════════════════════════════════════════════════════════════╗
// ║                             RSI                                      ║
// ╚══════════════════════════════════════════════════════════════════════╝

SCALE_RSI = esD ? 1.0 : esS ? 1.8 : 2.5
DELTA_RSI = -65
MULTI_RSI = 1.5

hline(showRSI ? DELTA_RSI : na, title='Línea Cero RSI', color=color.rgb(128, 128, 128), linestyle=hline.style_dashed, linewidth=1)
bandSobreventa  = hline(showRSI ? -10 * MULTI_RSI + DELTA_RSI : na, title='Línea Sobreventa', color=color.green, linestyle=hline.style_dotted)
bandSobrecompra = hline(showRSI ? 10 * MULTI_RSI + DELTA_RSI : na, title='Línea Sobrecompra', color=color.red, linestyle=hline.style_dotted)

rsiRaw   = ta.rsi(close, rsiLenPlot)
RSIMain  = (rsiRaw - 50) / SCALE_RSI
rsiMaRaw = ta.sma(rsiRaw, rsiMALen)
RSI_MA   = (rsiMaRaw - 50) / SCALE_RSI

rsiPlot = plot(showRSI ? RSIMain + DELTA_RSI : na, title='RSI', color=color.rgb(120, 12, 139), linewidth=2)
maPlot  = plot(showRSI ? RSI_MA + DELTA_RSI : na, title='Media RSI', color=color.yellow, linewidth=2)

// Gradiente de relleno: intensidad proporcional a distancia del RSI respecto a 50
rsiDiff         = RSIMain - RSI_MA
rsiBearIntensity = math.max(0.0, (50 - rsiRaw) / 50)  // 0 en RSI=50, 1 en RSI=0
rsiBullIntensity = math.max(0.0, (rsiRaw - 50) / 50)  // 0 en RSI=50, 1 en RSI=100
fillTransBear    = math.round(85 - rsiBearIntensity * 65)  // de 85 (tenue) a 20 (intenso) al caer
fillTransBull    = math.round(85 - rsiBullIntensity * 65)  // de 85 (tenue) a 20 (intenso) al subir
fillColor        = RSIMain >= RSI_MA ? color.new(#00bfa5, fillTransBull) : color.new(#ff1744, fillTransBear)
fill(rsiPlot, maPlot, color=showRSI ? fillColor : na, title='Relleno RSI vs MA')
fill(bandSobrecompra, bandSobreventa, color=color.new(#9915FF, 90), title='Fondo RSI')

// Mínimo y Máximo histórico del RSI (por papel)
rsiPlotVal = RSIMain + DELTA_RSI

var float rsiHistMin = na
var float rsiHistMax = na
if not na(rsiPlotVal)
    rsiHistMin := na(rsiHistMin) ? rsiPlotVal : math.min(rsiHistMin, rsiPlotVal)
    rsiHistMax := na(rsiHistMax) ? rsiPlotVal : math.max(rsiHistMax, rsiPlotVal)

var line rsiMinLine = na
var line rsiMaxLine = na
if barstate.islast and showRSI
    if not na(rsiMinLine)
        line.delete(rsiMinLine)
    rsiMinLine := line.new(bar_index - 1, rsiHistMin, bar_index, rsiHistMin, extend=extend.both, color=color.rgb(128, 128, 128), style=line.style_dotted, width=1)
    if not na(rsiMaxLine)
        line.delete(rsiMaxLine)
    rsiMaxLine := line.new(bar_index - 1, rsiHistMax, bar_index, rsiHistMax, extend=extend.both, color=color.rgb(128, 128, 128), style=line.style_dotted, width=1)
